@echo off

set APP=%0

if "%WINDDKBASE%"=="" goto define

if "%1"=="-?" goto usage
if "%1"=="/?" goto usage
if "%1"=="-h" goto usage
if "%1"=="/h" goto usage

if "%1"=="" goto usage
set TARGET_BUILD=%1
shift

if "%1"=="" goto usage
set TARGET_ARCH=%1
shift

if "%1"=="" goto usage
set TARGET_OS=%1
shift

if "%1"=="" goto usage
set TARGET_DIR=%1
shift

if NOT "%1"=="/f" goto noRebuild
set TARGET_REBUILD=/c
shift
:noRebuild

echo Target build: %TARGET_BUILD%
echo Target architecture: %TARGET_ARCH%
echo Target OS: %TARGET_OS%
echo Target directory: %TARGET_DIR%
echo Target rebuild: %TARGET_REBUILD%

: Save current directory
set OLDDIR=%cd%

: Set environment
call %WINDDKBASE%\bin\setenv %WINDDKBASE% %TARGET_BUILD% %TARGET_ARCH% %TARGET_OS% no_oacr

: Restore the old directory and go to source
chdir /d %OLDDIR%
cd %TARGET_DIR%

: Build it
call build %TARGET_REBUILD%

: Restore the old directory
chdir /d %OLDDIR%

goto :EOF

:define
@echo WINDDKBASE is not defined as environment varable. Please define it!
goto :EOF

:usage
@echo Usage: %APP% ^<chk^|fre^> ^<I386^|x64^> ^<WIN7^|WLH^|WXP^|WNET^> ^<directory^> [/r]
@echo Example: %APP% fre I386 WIN7 ..\Source /r
goto :EOF
